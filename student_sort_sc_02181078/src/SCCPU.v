`include "ctrl_encode_def.v"

module SCCPU(
    input         clk,
    input         reset,
    input  [31:0] inst_in,
    input  [31:0] Data_in,
    output        mem_w,
    output [31:0] PC_out,
    output [31:0] Addr_out,
    output [31:0] Data_out,
    input  [4:0]  reg_sel,
    output [31:0] reg_data
);

    wire        RegWrite;
    wire [5:0]  EXTOp;
    wire [4:0]  ALUOp;
    wire [2:0]  NPCOp_ctrl;
    wire [2:0]  NPCOp;
    wire [1:0]  WDSel;
    wire        ALUSrc;
    wire        Zero;
    wire [31:0] NPC;

    wire [6:0]  Op     = inst_in[6:0];
    wire [4:0]  rd     = inst_in[11:7];
    wire [2:0]  Funct3 = inst_in[14:12];
    wire [4:0]  rs1    = inst_in[19:15];
    wire [4:0]  rs2    = inst_in[24:20];
    wire [6:0]  Funct7 = inst_in[31:25];

    wire [11:0] iimm = inst_in[31:20];
    wire [11:0] simm = {inst_in[31:25], inst_in[11:7]};
    wire [11:0] bimm = {inst_in[31], inst_in[7], inst_in[30:25], inst_in[11:8]};
    wire [19:0] uimm = inst_in[31:12];
    wire [19:0] jimm = {inst_in[31], inst_in[19:12], inst_in[20], inst_in[30:21]};

    wire [31:0] immout;
    wire [31:0] RD1, RD2;
    wire [31:0] alu_A;
    wire [31:0] alu_B;
    wire [31:0] aluout;
    reg  [31:0] WD;

    assign Addr_out = aluout;
    assign Data_out = RD2;
    assign alu_A = (Op == 7'b0010111) ? PC_out : RD1; // auipc uses PC as ALU A
    assign alu_B = ALUSrc ? immout : RD2;
    assign NPCOp = (NPCOp_ctrl == `NPC_BRANCH && !Zero) ? `NPC_PLUS4 : NPCOp_ctrl;

    ctrl U_ctrl(
        .Op(Op), .Funct7(Funct7), .Funct3(Funct3), .Zero(Zero),
        .RegWrite(RegWrite), .MemWrite(mem_w),
        .EXTOp(EXTOp), .ALUOp(ALUOp), .NPCOp(NPCOp_ctrl),
        .ALUSrc(ALUSrc), .WDSel(WDSel)
    );

    PC U_PC(
        .clk(clk),
        .rst(reset),
        .NPC(NPC),
        .PC(PC_out)
    );

    NPC U_NPC(
        .PC(PC_out),
        .NPCOp(NPCOp),
        .IMM(immout),
        .RS1(RD1),
        .NPC(NPC)
    );

    EXT U_EXT(
        .iimm(iimm),
        .simm(simm),
        .bimm(bimm),
        .uimm(uimm),
        .jimm(jimm),
        .EXTOp(EXTOp),
        .immout(immout)
    );

    RF U_RF(
        .clk(clk),
        .rst(reset),
        .RFWr(RegWrite),
        .A1(rs1),
        .A2(rs2),
        .A3(rd),
        .WD(WD),
        .RD1(RD1),
        .RD2(RD2),
        .reg_sel(reg_sel),
        .reg_data(reg_data)
    );

    alu U_alu(
        .A(alu_A),
        .B(alu_B),
        .ALUOp(ALUOp),
        .C(aluout),
        .Zero(Zero)
    );

    always @(*) begin
        case (WDSel)
            `WDSel_FromALU: WD = aluout;
            `WDSel_FromMEM: WD = Data_in;
            `WDSel_FromPC:  WD = PC_out + 32'd4;
            default:        WD = aluout;
        endcase
    end
endmodule
